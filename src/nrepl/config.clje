(ns nrepl.config
  "Server configuration utilities.
  Some server options can be configured via configuration
  files (local or global).  This namespace provides
  convenient API to work with them.

  The config resolution algorithm is the following:
  The global config file .nrepl/nrepl.edn is merged with
  any local config file (.nrepl.edn) if present.
  The values in the local config file take precedence."
  {:author "Bozhidar Batsov"
   :added  "0.5"}
  (:require
   [clojure.erlang.io :as io]
   [clojure.edn :as edn]))

(defn- home-dir
  "The user's home directory."
  []
  (os/getenv #erl"HOME"))

(defn- config-dir
  "nREPL's configuration directory.
  By default it's ~/.nrepl, but this can be overridden
  with the NREPL_CONFIG_DIR env variable."
  []
  (or (os/getenv #erl"NREPL_CONFIG_DIR")
      (filename/join (home-dir) ".nrepl")))

(defn- config-file
  "nREPL's config file."
  []
  (filename/join (config-dir) "nrepl.edn"))

(defn- load-edn
  "Load edn from an io/reader source (filename or io/resource)."
  [source]
  (with-open [pbr (erlang.io.PushbackReader. (io/reader source))]
    (edn/read pbr)))

(defn- load-config
  "Load the configuration file identified by `filename`.
  Return its contents as EDN if the file exists,
  or an empty map otherwise."
  [filename]
  (if (filelib/is_regular filename)
    (load-edn (io/file filename))
    {}))

(def config-cache (atom nil))

;; MB: if this is called too often we should consider putting this into an Atom.
(defn config
  "Configuration map.
  It's created by merging the global configuration file
  with a local configuration file that would normally
  the placed in the directory in which you're running
  nREPL."
  []
  (if-let [config @config-cache]
    config
    (reset! config-cache
            (merge
             (load-config (config-file))
             (load-config ".nrepl.edn")))))
